/********************************************************
 * OSCake / OSCD = Open Source Compliance Description
 * Open Source Compliance artifacts knowledge engine
 * 
 * Copyright (c) {2020 Karsten Reincke, Deutsche Telekom AG
 *
 * This program is made available under the terms of the
 * Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0
 *
 * SPDX-License-Identifier: EPL-2.0
  *******************************************************/

grammar de.oscake.strict.Oscd with org.eclipse.xtext.common.Terminals

generate oscd "http://www.oscake.de/strict/Oscd"

/**
 * A strict Compliance Artifact Collection consist of
 * a) Meta-Data
 * b) a list of complianceArtifactPackages (each referring to a software package)
 * c) a list of not instantiated = multiply usable license texts
 */
ComplianceArtifactCollection:
'{'
  '"complianceArtifactCollection"' ':' '{'
    '"cid"' ':' cacid=STRING ','
    ('"author"' ':' cacComposer=STRING ',')?
    ('"release"' ':' cacReleaseNumber=STRING ',')?
    ('"date"' ':' cacReleaseDate=STRING ',')?  
    '"containerPath"' ':' cacContPath=STRING ','
    '"containerType"' ':' cacContType=('"ZIP"'|'"DIR"')
  '}' ','

  '"complianceArtifactPackages"' ':' '['
    '{'
      (complianceArtifactPackages+=ComplianceArtifactPackage '}' ',' '{')*
      (complianceArtifactPackages+=ComplianceArtifactPackage '}')
  ']' ','
  '"multiplyUsableFossLicenses"' ':' '['
    (
      '{'
        (multiplyUsableFossLicenses+=MultiplyUsableFossLicense '}' ',' '{')* 
        (multiplyUsableFossLicenses+=MultiplyUsableFossLicense '}')
    )?
  ']' 
'}';

/**
 * Each ComplianceArtifactPackage refers to one software package of the 
 * distributable collection 
 */

ComplianceArtifactPackage:
  '"pid"' ':' capid=STRING ','
  '"release"' ':' capReleaseNumber=STRING ',' 
  '"repository"' ':' capRepoUrl=STRING ',' 
  
  '"defaultLicensings"' ':' '['
      '{'
       (defComplianceArtifactSets+=DefComplianceArtifactSet'}' ',' '{')* 
       (defComplianceArtifactSets+=DefComplianceArtifactSet '}')
  ']' ',' 
  '"dirLicensings"' ':' '['
    (
      '{'
       (dirComplianceArtifactSets+=DirComplianceArtifactSet '}' ',' '{')* 
       (dirComplianceArtifactSets+=DirComplianceArtifactSet '}')
    )?
  ']' ',' 
  '"fileLicensings"' ':' '['
    (
      '{'
        (fileComplianceArtifactSets+=FileComplianceArtifactSet '}' ',' '{')* 
        (fileComplianceArtifactSets+=FileComplianceArtifactSet '}')
    )?
  ']'  
;
 
DefComplianceArtifactSet:
  '"defaultScope"' ':' dstatus=('"MAIN"'|'"ALT"') ','
  cas=ComplianceArtifactSet 
;

 DirComplianceArtifactSet:
  '"dirScope"' ':' dpath=STRING ','
  cas=ComplianceArtifactSet
;

FileComplianceArtifactSet:
  '"fileScope"' ':' fpath=STRING ','
  cas=ComplianceArtifactSet
;
 
 
 ComplianceArtifactSet:
  '"license"' ':'
  ( ( spdxId='"MIT"' ',' casMit=ComplianceArtifactSetMIT )|
     (spdxId='"BSD-2-Clause"' ',' casBsd2Cl=ComplianceArtifactSetBSD2Clause) |
     (spdxId='"BSD-3-Clause"' ',' casBsd3Cl=ComplianceArtifactSetBSD3Clause) |
     (spdxId='"Apache-2.0"' ',' casApache20=ComplianceArtifactSetApache20) 
   )
;
ComplianceArtifactSetMIT:
  '"licenseText"' ':' lfPath=(STRING | 'Null') /* TODO TEST & FIX! */
;

ComplianceArtifactSetBSD2Clause:
  /*'"licenseText"' ':' (lfPath=STRING | nil='Null')*/
  '"licenseText"' ':' lfPath=(STRING | 'Null')
;

ComplianceArtifactSetBSD3Clause:
  /*'"licenseText"' ':' (lfPath=STRING | nil='Null')*/
  '"licenseText"' ':' lfPath=(STRING | 'Null')
;

ComplianceArtifactSetApache20:
  /*'"licenseText"' ':' (lfPath=STRING | nil='Null')*/
  '"licenseText"' ':' apl='"multiply-usable"' ','
  '"noticeText"' ':' nfPath=(STRING | 'Null')
; 

MultiplyUsableFossLicense:
  '"license"' ':'  
  spdxId= ( '"Apache-2.0"' | 
            '"Apache-1.0"'
          ) ','
  '"licenseText"' ':' lfPath=STRING 
;
