/********************************************************
 * OSCake / OSCF = Open Source Compliance Description
 * Open Source Compliance artifacts knowledge engine
 * 
 * Copyright (c) {2020 Karsten Reincke, Deutsche Telekom AG
 *
 * This program is made available under the terms of the
 * Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0
 *
 * SPDX-License-Identifier: EPL-2.0
  *******************************************************/

grammar de.oscake.strict.Oscf with org.eclipse.xtext.common.Terminals

generate oscf "http://www.oscake.de/strict/Oscf"

/**
 * A strict Compliance Artifact Collection consist of
 * a) Meta-Data
 * b) a list of complianceArtifactPackages (each referring to a software package)
 * c) a list of not instantiated = multiply usable license texts
 */
ComplianceArtifactCollection:
'{'
  '"complianceArtifactCollection"' ':' '{'
    '"cid"' ':' cacid=STRING ','
    ('"author"' ':' cacComposer=STRING ',')?
    ('"release"' ':' cacReleaseNumber=STRING ',')?
    ('"date"' ':' cacReleaseDate=STRING ',')? 
    /*
     * Note: in the generator is defined an absolute path to a general data repository / directory
     * contentDir is a directory inside of the general data repository.
     * In this content directory, the evaluating xtend program expects the
     * files found by ORT which must be integrated into the final OSCF
     */ 
    '"contentDir"' ':' cacContDir=STRING 
  '}' ','

  '"complianceArtifactPackages"' ':' '['
    '{'
      (complianceArtifactPackages+=ComplianceArtifactPackage '}' ',' '{')*
      (complianceArtifactPackages+=ComplianceArtifactPackage '}')
  ']' 
'}';

/**
 * Each ComplianceArtifactPackage refers to one software package of the 
 * distributable collection 
 */

ComplianceArtifactPackage:
  '"pid"' ':' capid=STRING ','
  '"release"' ':' capReleaseNumber=STRING ',' 
  '"repository"' ':' capRepoUrl=STRING ',' 
  
  '"defaultLicensings"' ':' '['
    '{'
      (casl+=ComplianceArtifactSet '}' ',' '{')* 
      (casl+=ComplianceArtifactSet '}')    
  ']' ',' 
  
  '"dirLicensings"' ':' '['
    (
      '{'
       (dirComplianceArtifactSets+=DirComplianceArtifactSet '}' ',' '{')* 
       (dirComplianceArtifactSets+=DirComplianceArtifactSet '}')
    )?
  ']' ',' 
  '"fileLicensings"' ':' '['
    (
      '{'
        (fileComplianceArtifactSets+=FileComplianceArtifactSet '}' ',' '{')* 
        (fileComplianceArtifactSets+=FileComplianceArtifactSet '}')
    )?
  ']'  
;
 
 DirComplianceArtifactSet:
  '"dirScope"' ':' dpath=STRING ','
  '"dirLicenses"' ':' '['
    '{'
      (dcasl+=ComplianceArtifactSet '}' ',' '{')* 
      (dcasl+=ComplianceArtifactSet '}') 
  ']'
;

/*
 * ACHTUNG: nur übergangshalber. In der Endausbaustufe erscheinen
 * Notice-Dateien nicht als eigenständige Dateien in der Fileliste.
 * Aber ihr inArchivePath erscheint im ComplianceArtifactSetApache20
 * 
 * Es ist Aufgabe des OSCC-Generators dieses Verhalten zu implementieren.
 * 
 */
FileComplianceArtifactSet:
  
  '"fileScope"' ':'
  ( ( fpath=('"NOTICE"' | '"NOTICE.txt"' | '"NOTICE.md"') ','
    '"fileContentInArchive"' ':' fcPath=('null' | '"911"' | STRING  ) 
    )
    |
    ( fpath=STRING )
  ) ','
  
  '"fileLicenses"' ':' '['
    '{'
      (fcasl+=ComplianceArtifactSet '}' ',' '{')* 
      (fcasl+=ComplianceArtifactSet '}')
   ']'
   
;
 
 
 ComplianceArtifactSet:
  '"license"' ':'
  ( ( spdxId='"MIT"' ',' casMit=ComplianceArtifactSetMIT )|
     (spdxId='"BSD-2-Clause"' ',' casBsd2Cl=ComplianceArtifactSetBSD2Clause) |
     (spdxId='"BSD-3-Clause"' ',' casBsd3Cl=ComplianceArtifactSetBSD3Clause) |
     (spdxId='"Apache-2.0"' ',' casApache20=ComplianceArtifactSetApache20) |
     (spdxId='"EPL-1.0"' ',' casEpl10=ComplianceArtifactSetEpl10) |
     (spdxId='"EPL-2.0"' ',' casEpl20=ComplianceArtifactSetEpl20) |
     (spdxId='"LGPL-2.0-or-later"' ',' casLgpl20=ComplianceArtifactSetLgpl20) |
     (spdxId='"LGPL-2.1-or-later"' ',' casLgpl21=ComplianceArtifactSetLgpl21) |
     (spdxId='"noassertion"' ',' casNoAssert=ComplianceArtifactSetNoAssert) 
   )
;

ComplianceArtifactSetNoAssert:
  '"licenseType"' ':' ltype='"instantiated"' ','
  '"licenseTextInArchive"' ':' lfPath=(STRING | 'null' | '"911"') 
;


ComplianceArtifactSetMIT:
  '"licenseType"' ':' ltype='"instantiated"' ','
  '"licenseTextInArchive"' ':' lfPath=(STRING | 'null' | '"911"') 
;

ComplianceArtifactSetBSD2Clause:
  '"licenseType"' ':' ltype='"instantiated"' ','
  '"licenseTextInArchive"' ':' lfPath=(STRING | 'null' | '"911"')
;

ComplianceArtifactSetBSD3Clause:
  '"licenseType"' ':' ltype='"instantiated"' ','
  '"licenseTextInArchive"' ':' lfPath=(STRING | 'null' | '"911"')
;

ComplianceArtifactSetApache20:
  '"licenseType"' ':' ltype='"multiply-usable"' ','
  '"apacheNoticeTextInArchive"' ':' nfPath=(STRING | 'null' | '"911"')
; 

ComplianceArtifactSetEpl10:
  '"licenseType"' ':' ltype='"multiply-usable"' 
;

ComplianceArtifactSetEpl20:
  '"licenseType"' ':' ltype='"multiply-usable"' 
;

ComplianceArtifactSetLgpl20:
  '"licenseType"' ':' ltype='"multiply-usable"' 
; 

ComplianceArtifactSetLgpl21:
  '"licenseType"' ':' ltype='"multiply-usable"' 
; 
